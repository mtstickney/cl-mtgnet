(in-package #:cl-mtgnet)

(defun encode-field (field value &optional (encoder #'json:encode-json))
  "Encode VALUE as an object field named FIELD, using ENCODER to
encode VALUE."
  (json:as-object-member (field)
    (funcall encoder value)))


(eval-when (:compile-toplevel :load-toplevel :execute)
  (defun serial-slot-p (s)
    (or (symbolp s)
        (and (listp s)
             (destructuring-bind (name &key (marshall t) &allow-other-keys)
                 s
               (declare (ignore name))
               marshall))))

  (defun optional-slot-p (s)
    (and (listp s)
         (destructuring-bind (name &key optional &allow-other-keys)
             s
           (declare (ignore name))
           optional)))

  (defun slot-symbol (s)
    (etypecase s
      (symbol s)
      (list (destructuring-bind (name &rest r) s
              (declare (ignore r))
              name))))

  (defun cat-symbol (symbol &rest rest)
  (intern (format nil "窿磲疸狎＇簌礅镬钺礤ㄣ镱簌礅镬蝈篝┅┅ㄤ彐躅箪雉镳糸镱犰簌礅镬镳糸镱犰箪雉豉疱ㄣ桢汶豉疱簌礅镬簌礅镬ㄣ桢汶豉疱镳糸镱犰箪雉扉篝ㄣ桢汶豉疱豉疱脲黠蜾戾è沐祆ㄡ篌镢簌礅镬镳糸镱犰箪雉螬┅ㄡ钿沐祆ㄥ汜箦豉疱ê黩轸矧ㄥㄣ潋沐祆瑚蜷翦ㄥㄣ潋沐祆候遽洵黩轸濠┅ê蝈徜矧ㄥㄣ潋沐祆候遽洎ㄥㄣ潋沐祆候遽洵黩轸濠┅ê蝈徜黩轸ㄥㄣ潋沐祆候遽洵黩轸濠┅┅┅换韵南渝殒轸痫篌殁戾麸珏铄蜥翦滹泱趄轭珞骘翳弩翳轭珞ㄤ彐磲泸溴骈铄牦镱镡钺礤怙澌怙澌戾舄è箪雉ㄩ篝蜷铉ㄦ轵篝怙澌┅ㄦ轵篝ㄣ潋怙澌┅ㄦ轵篝怙澌┅箦蜷犰辁邃箪雉磲疸狎＇箪雉簌礅镬蝈盹鲥殒铒＇箦蜷犰箪雉箪雉螬┅镳糸镱犰箪雉蝈盹鲥殒＇铛祆磲疸狎灬礅溽螬戾è镳糸镱犰镳糸镱犰箪雉螬┅ㄩ镳糸镱犰ㄣ镱箪雉簌礅镬螬镳糸镱犰铋飑┅箪雉螬┅磲脲骢钽ㄣ狒簌礅镬В喉犭瀛钺礤┅ㄢ蹰熹骢钽ㄣ狒簌礅镬В衡蹰熹钺礤┅躅磲蝮栳祆骢钽ㄣ狒簌礅镬В乎铐狎箬犰飙钺礤┅磲蝮栳祆骢钽ㄣ狒簌礅镬В喉狎箬犰飙钺礤┅啜痱镧ㄤ彐篝蝓泗钺礤换橡糸镱犰滹泱趄轭括殒篝蜷铉ㄦ轵篝怙澌┅扉篝ㄦ轵篝怙澌┅铋飑换屿雉扉篝括祜镳骘箪雉轭箪雉泔祆邈ㄥ豉疱汜箦箪雉簌礅镬箪雉扉篝ㄤ弩趄蹉趱蜷铉忾钿钺礤蝈篝脲黠蜾疳轵脲轭轸獒犰祜鳝雉桢颦脲螬箪雉啜钺礤轭轸獒横祆秣雉桢颦脲离妁黠蜾疳轵螬┅┅换序镤蹉篝蝓泗躜骝镯溴泔溴牦镱镡赍泗ㄤ彐躅怩殪洵骢钽牦镱镡戛戾è狎珈轶Ж┅括祜镳骘轭箦蜷犰辁邃箪雉泔祆邈啜戾è沐祆ㄡ篌镢牦镱脲К螬牦镱镡戛┅括躅戾篌箪雉镳糸镱犰镳糸镱犰箪雉候遽洎啜躅戾篌沐祆ㄥ蝌矧ч铞犰殇牦镱镡呼疱К钺礤宏箫牦镱镡戛┅麒孱沐祆箦翩ㄧ弭狎珈轶ㄩ铘弪簌礅镬钺礤螬щ妁黠蜾┅ㄣ潋沐祆┅┅ㄡ痧禊＇磲脲骢钽狎珈轶舂┅ㄤ彐躅躅磲蝮栳祆骢钽牦镱篝蜷铉戾è牦镱镡牦镱轰邈镤瀛牦镱骝镯篝蜷铉牦镱篝蜷铉┅ì怩殪洵骢钽牦镱镡戛┅ㄤ彐躅磲蝮栳祆骢钽镡戛牦镱瑚轸璀镡赍泗ī括骒弭è徙沐篌矧ㄦ殄熹ㄩ铘弪ㄦ矧磲铋镰立钺礤骈屐洎┅祜镳骘轭箦蜷犰辁邃箪雉泔祆邈戾è孱泔溴骘蝽啜孱泔溴骈屐Кìㄡ沣弩箫螬镡戛┅ㄩ箪雉镳糸镱犰镳糸镱犰箪雉瑚蜷翦啜麒孱ìㄡ沣弩箫螬镡戛孱泔溴骘蝽孱泔溴骘蝽┅┅┅┅ㄤ彐轭瀛牦镱镡蝠悱汜祆⒘箝铉戾礤翳镤轭鲲汜糸镱箦蝣殂洪铋糸犰呼疱篝蜷铉候遽洵镱禊舂礤翳镤洪铋糸犰呼疱篝蜷铉候遽洵镱禊舂ㄡ蜱洪铋糸犰Ж呼疱狎珈轶候遽洵镱禊舂ㄩ洪铋糸犰铋候遽洵镱禊舂